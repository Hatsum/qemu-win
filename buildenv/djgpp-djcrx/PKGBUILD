# Contributor: Andris Pavenis <andris.pavenis iki fi>
# Contributor: carstene1ns <arch carsten-teibes de>
# Contributor: felix <base64 -d <<< ZmVsaXgudm9uLnNAcG9zdGVvLmRlCg==>
# Contributor: Schala

_realname=djgpp-djcrx
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.05
_target='i686-pc-msdosdjgpp'
pkgrel=1
pkgdesc="Headers and utilities for the djgpp cross-compiler"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64')
url="http://www.delorie.com/djgpp/"
license=('GPL' 'LGPL' 'custom:djgpp')
groups=("${MINGW_PACKAGE_PREFIX}-djgpp-toolchain")
makedepends=("${MINGW_PACKAGE_PREFIX}-djgpp-gcc"
             "${MINGW_PACKAGE_PREFIX}-djgpp-binutils")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
conflicts=('${MINGW_PACKAGE_PREFIX}-djgpp-djcrx-bootstrap')
options=('!buildflags' '!strip')
source=("http://ftp.xtal.net/pub/mirrors/djgpp/current/v2/djcrx${pkgver//./}.zip"
        "http://ftp.xtal.net/pub/mirrors/djgpp/current/v2/djlsr${pkgver//./}.zip"
        "ttyscrn.patch"
        "nmemalign.patch"
        "fseeko64.patch"
        "asm.patch"
        "dxegen.patch"
        "djgpp-djcrx-gcccompat.patch"
        "gcc-12.patch"
        "mingw.patch")
sha256sums=('22274ed8d5ee57cf7ccf161f5e1684fd1c0192068724a7d34e1bde168041ca60'
            '80690b6e44ff8bc6c6081fca1f4faeba1591c4490b76ef0ec8b35847baa5deea'
            '83bc02407566c0613c2eeb86d78f2968c11256dfc8d3c2805a5488540e059124'
            'ffdbab52eb13dde716fbcce54154f7c95644afd6333f4931b2cceeb95935c675'
            '536684b0152f7ad77b99bcc5ea535ca8339832399c4582b944ccd882e4b261a1'
            '693810c3242f4e23cdc55d3101281721da9407851e5d29459ad59405e534b916'
            '0debe0161e27aeb004e89a43915d6d77bcd07a5db2c67e2798568535fe9143f1'
            '8c16b24e34dc46fee86531349a84d7353552db2eff008cb9d5ffd35ec3a7249a'
            'SKIP'
            'SKIP')

prepare() {
  cd ${srcdir}
  
  sed -i "s/i586-pc-msdosdjgpp/$_target/" src/makefile.def src/dxe/makefile.dxe
  sed -i 's/ln/ln -f/' src/dxe/makefile.dxe

  # fix build with gcc >= 8 
  patch -Np1 < djgpp-djcrx-gcccompat.patch

  # gcc provides its own float.h which masks this one
  ln -fs float.h include/djfloat.h
  sed -i 's/<float\.h>/<djfloat.h>/' src/libc/{go32/dpmiexcp,emu387/npxsetup}.c src/utils/redir.c

  # fix libc bugs
  patch -Np0 < ttyscrn.patch
  patch -Np0 < nmemalign.patch
  patch -Np0 < fseeko64.patch
  patch -Np0 < asm.patch

  # allow using dxe3gen without DJDIR and without dxe3res in PATH
  patch -Np0 < dxegen.patch
  
  # fix build with gcc >= 12
  patch -Np1 < gcc-12.patch
  
  # fix build with mingw
  patch -Np1 < mingw.patch

  # be verbose
  #sed -i '/@$(MISC) echo - / d; s/^\t@/\t/' src/makefile.inc src/libc/makefile src/utils/makefile
}

build() {
  cd ${srcdir}/src
  
  export MSYS2_ARG_CONV_EXCL="*"
  
  make clean
  make

  cd dxe
  make -f makefile.dxe
}

package() {
  cd ${srcdir}
  
  install -d ${pkgdir}${MINGW_PREFIX}/bin
  install -d ${pkgdir}${MINGW_PREFIX}/${_target}/bin
  install -d ${pkgdir}${MINGW_PREFIX}/${_target}/sys-include

  cp -r include/* ${pkgdir}${MINGW_PREFIX}/${_target}/sys-include
  cp -r lib ${pkgdir}${MINGW_PREFIX}/${_target}

  cd hostbin
  for _file in djasm mkdoc stubedit stubify; do
    install -m0755 ${_file}.exe ${pkgdir}${MINGW_PREFIX}/${_target}/bin/${_file}.exe
    cp ${pkgdir}${MINGW_PREFIX}/${_target}/bin/${_file}.exe ${pkgdir}${MINGW_PREFIX}/bin/${_target}-${_file}
  done

  cd ../src/dxe
  for _file in bin2h dxe3gen dxe3res; do
    install -m0755 ${_file}.exe ${pkgdir}${MINGW_PREFIX}/${_target}/bin/${_file}.exe
    cp ${pkgdir}${MINGW_PREFIX}/${_target}/bin/${_file}.exe ${pkgdir}${MINGW_PREFIX}/bin/${_target}-${_file}.exe
  done

  cd "$srcdir"
  install -Dm644 copying.dj ${pkgdir}${MINGW_PREFIX}/share/licenses/${_target}/${_realname}/copying.dj

  cd info
  for _file in *.info; do
    install -Dm0644 ${_file} ${pkgdir}${MINGW_PREFIX}/share/info/${_target}/djgpp-${_file}
  done
}
